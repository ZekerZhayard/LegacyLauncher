apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

String getBuildNum() {
    if (System.getenv("CIRCLE_BUILD_NUM") != null)
        return System.getenv("CIRCLE_BUILD_NUM")
    return "00"
}

group = 'customskinloader'
archivesBaseName = 'launchwrapper'
version = '2.1.' + getBuildNum()
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    compile 'org.apache.logging.log4j:log4j-api:2.11.0'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

jar {
    manifest {
        attributes(
            'Implementation-Version' : version,
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Created-By'     : "Gradle ${gradle.gradleVersion}"
        )
    }
}

apply from: rootProject.file('cos.gradle')

task upload {
    onlyIf { System.getenv("COS_SECRET_KEY") != null }
    doLast {
        uploadArtifacts(version)
    }
}